# vim:ft=zsh

# Path to your oh-my-zsh configuration.
export ZSH=$HOME/.oh-my-zsh

# Set to the name theme to load.
# Look in ~/.oh-my-zsh/themes/
export ZSH_THEME="arrow"

# Set to this to use case-sensitive completion
# export CASE_SENSITIVE="true"

# Comment this out to disable weekly auto-update checks
export DISABLE_AUTO_UPDATE="true"

# Uncomment following line if you want to disable colors in ls
# export DISABLE_LS_COLORS="true"

# Which plugins would you like to load? (plugins can be found in ~/.oh-my-zsh/plugins/*)
# Example format: plugins=(rails git textmate ruby lighthouse)
plugins=(archlinux git git-flow autojump npm mvn nvm node history)

source $ZSH/oh-my-zsh.sh

# Do not persist commands starting with *space* in zsh history
setopt HIST_IGNORE_SPACE

# Enable exotic globbing
setopt extendedglob

# `gm` alias from oh-my-zsh/git conflicts with GraphicsMagick
unalias gm

# Re-bind edit-command-line
# (for some reason, the binding gets lost)
bindkey "\C-x\C-e" edit-command-line

# Customize to your needs...
export EDITOR=vim
export SHELL=zsh
export BROWSER=google-chrome-beta

# Prevent oh-my-zsh from auto-updating window titles
export DISABLE_AUTO_TITLE=true

# grep dropped support for GREP_OPTIONS
export GREP_OPTIONS=

# run command in a new tmux pane, while keeping
# the cursor at the current shell
# e.g. tmw watch -n 1 uptime
function tmw() {
   tmux split-window -d -p 80 "$*"
}

# SSH to multiple hosts and
# turn on synchronization
function tssh() {
  if [ $1 == "-g" ];
  then
    tssh $(pdsh -q "$@" | tail -1 | sed -e 's/,/ /g')
  else
    for (( i=$#; i>0; i-- ));
    do
      tmux split-window -d -p$((100 / $i)) "ssh $1"
      shift;
    done
    tmux setw synchronize-panes
    exit
  fi
}

v() { vim =($@) }
nautilus() { command nautilus --no-desktop > /dev/null 2>&1 "$@" &! }
reboot() { sudo umount -a -t cifs && sudo reboot $@ }

yaweb() {
   if [ -z "$*" ]; then return; fi
   (yaourt -Si $@ | grep URL | cut -f2,3 -d: | xargs --no-run-if-empty $BROWSER) 2>/dev/null
}

# Generates numerical stats based on the following input format:
#    Label1 x1
#    Label2 x2
#    Label1 x3
#    ...
# Output format:
#    Label1 OCCURENCES1 MIN1 MAX1 OCCURENCES1
#    ...
# Output is sorted by occurences
alias awkminmax="awk '{ if (min[\$1]>\$2 || min[\$1]==0) min[\$1]=\$2; \
                       if (max[\$1]<\$2) max[\$1]=\$2; \
                       n[\$1]++ } \
                     END \
                     { for (i in n) {print i, n[i], min[i], max[i]} }' \
                | column -t | sort -k2nr"

# Generates numerical stats based on the following input format:
#    Label1 x1
#    Label2 x2
#    Label1 x3
#    ...
# Output format:
#    MIN1 AVG1 STANDARD_DEVIATION_1 MAX1 OCCURENCES1 Label1 
#    ...
# Output is sorted by AVG column
alias awkstats="awk '{ if (min[\$1]>\$2 || min[\$1]==0) min[\$1]=\$2; \
                       if (max[\$1]<\$2) max[\$1]=\$2; \
                       sum[\$1]+=\$2; sumsq[\$1]+=\$2**2; n[\$1]++
                     } \
                     END \
                     { for (i in n) { \
                        avg=sum[i]/n[i]; \
                        avgsq=sumsq[i]/n[i]; \
                        avg2=avg**2; delta=(avgsq>avg2)? avgsq-avg2 : avg2-avgsq; \
                        stddev=sqrt(delta); \
                        print min[i], avg, stddev, max[i], n[i], i } \
                     }' \
                | column -t | sort -k2nr"

# (Much) Faster equivalent to 'sort | uniq -c'
alias uniqc="awk '{ s[\$1]++ } END { for (i in s) {print s[i], i} }' | column -t"

# Filter Apache access logs based on HTTP status
# Retain URL and processing time in ms
alias apacheok="awk '{ if (\$9<400 && \$9!=404) print \$7, \$10/1000 }'"
alias apacheerr="awk '{ if (\$9>400 && \$9!=404) print \$9\":\"\$7, \$10/1000 }'"

# Normalize URLs in Apache access logs for easier stats
alias squashurls="sed -e 's/\/[0-9]\+/\/_ID_/' -e 's/q=[a-zA-Z\+\%]\+/q=_PARAM_/'"

alias ggpull='git pull --rebase origin $(current_branch)'
alias ggpnp='git pull --rebase origin $(current_branch) && git push origin $(current_branch)'
alias ggl='git log --decorate --oneline --graph --all -20'
gitweb() {
   branch=$(current_branch) || return
   $BROWSER $(sed -e "s#^.*\(git[^\.]*\.[^:\.]*\)[:\.]#http://\1/#" -e "s#\.git#/tree/$(current_branch)#" <(git config --get remote.origin.url)) 2>& /dev/null
}

alias printxml='xmllint --format -'
alias printjson='python -m json.tool'
alias ll='ls -lh'

# Expands into JS source files under current directory
# Excludes any files under node_modules, bower_components, or dist
#
# Example usage: jshint jss
alias -g jss='**/*.js~(*node_modules|*bower_components|dist|*styleguide)/*'

alias deps='rm -rf node_modules && npm install'
alias bi='bundle install --path=vendor/bundle --binstubs'

jkill() { jps 2>/dev/null | grep -i $1 | awk '{print $1}' | xargs kill -9 }

pdfslice() {
   # Usage: pdfslice ~/path/to/file.pdf a[-b]
   # Crops pages a[-b] from file.pdf into ~/path/to/filea[-b].pdf
   #
   # Requires 'psutils' and 'poppler' packages
   #
   pdftops $1 - | psselect -p$2 | ps2pdf - ${1:r}$2.pdf
}

mvn() {
   PURPLE=$(echo -en "\033[35m")
   GREEN=$(echo -en "\033[32m")
   RED=$(echo -en "\033[31m")
   YELLOW=$(echo -e "\033[33m")
   NORMAL=$(echo -e "\033[0m")
   command mvn $* | sed \
           -e "s/^\(.ERROR. .*\)$/$RED\1$NORMAL/" \
           -e "s/^\([^\[].*\)/${YELLOW}\1${NORMAL}/" \
           -e "s/^\(.INFO. ----.*\)$/$PURPLE\1$NORMAL/" \
           -e "s/^\(.INFO. Building.*SNAPSHOT\)$/$PURPLE\1$NORMAL/" \
           -e "s/^\(.INFO. --- .* ---.*\)$/$PURPLE\1$NORMAL/"\
           -e "s/^\(.INFO. .*\)$/$GREEN\1$NORMAL/" \
           -e "s/^\(.WARNING. .*\)$/$YELLOW\1$NORMAL/"
}

alias mci='mvn clean install -Dmaven.test.skip=true -T2'
export M2_MEM="-Xms512m -Xmx768m -XX:PermSize=256m -XX:MaxPermSize=256m"
export M2_DEBUG="$M2_MEM -Dmaven.test.skip=true -Xdebug -Xnoagent -Djava.compiler=NONE -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000"
export M2_DEBUG_ALT="${M2_DEBUG/8000/4000}"

export SBT_OPTS=$M2_MEM

export IPAD_AGENT="Mozilla/5.0 (iPad; U; CPU OS 3_2 like Mac OS X; en-us) AppleWebKit/531.21.10 (KHTML, like Gecko) Version/4.0.4 Mobile/7B334b Safari/531.21.10"

alias vpnconnect='sudo openconnect -q -b --pid-file /var/run/vpnc/pid --script vpnc-script -u tsokhon --no-cert-check 92.103.100.180 2>&1 > /dev/null'
alias vpndisconnect='sudo kill -15 $(< /var/run/vpnc/pid )'
socks() {
  gsettings set org.gnome.system.proxy.socks host 'localhost'
  gsettings set org.gnome.system.proxy.socks port ${@:-4320}
  gsettings set org.gnome.system.proxy mode 'manual' 
}
alias unsocks="gsettings set org.gnome.system.proxy mode 'none'"

alias minecraft='java -Xmx1024M -Xms512M -cp ~/minecraft.jar net.minecraft.LauncherFrame'

export PATH=~/.gem/ruby/2.0.0/bin:~/.cabal/bin:/usr/local/bin:$PATH
export NODE_PATH=/usr/lib/node_modules

uname | grep -q Darwin && source ~/.zshrc.mac

if [[ -f /usr/bin/tmux || -f /usr/local/bin/tmux ]]; then
  # run tmux in 256-colors compatibility mode
  tmux() {
    TERM=screen-256color-bce command tmux "$@"
  }

  # attach to an already running tmux session or create new one
  # unless we're running a login shell in a virtual console or via SSH
  tty | grep -qo /dev/tty.$ || test "$SSH_TTY" || test "$TMUX" || tmux attach 2>/dev/null || tmux new-session
fi
