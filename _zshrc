# vim:ft=zsh

# Path to your oh-my-zsh configuration.
export ZSH=$HOME/.oh-my-zsh

# Set to the name theme to load.
# Look in ~/.oh-my-zsh/themes/
export ZSH_THEME="arrow"

# Set to this to use case-sensitive completion
# export CASE_SENSITIVE="true"

# Comment this out to disable weekly auto-update checks
export DISABLE_AUTO_UPDATE="true"

# Uncomment following line if you want to disable colors in ls
# export DISABLE_LS_COLORS="true"

# Which plugins would you like to load? (plugins can be found in ~/.oh-my-zsh/plugins/*)
# Example format: plugins=(rails git textmate ruby lighthouse)
plugins=(git npm nvm node yarn history docker)

source $ZSH/oh-my-zsh.sh

# Do not persist commands starting with *space* in zsh history
setopt HIST_IGNORE_SPACE
# Set up history sharing
setopt hist_expire_dups_first
setopt sharehistory

# Enable exotic globbing
setopt extendedglob

ulimit -n 2048

# Increase Zsh history size
export HISTSIZE=100000
export SAVEHIST=$HISTSIZE


# `gm` alias from oh-my-zsh/git conflicts with GraphicsMagick
unalias gm

# Re-bind edit-command-line
# (for some reason, the binding gets lost)
bindkey "\C-x\C-e" edit-command-line

# <c-z> runs fg
fg_widget() fg
zle -N fg_widget
bindkey '^z' fg_widget

# Customize to your needs...
export EDITOR=nvim
alias -g vim=nvim
export SHELL=zsh
if uname | grep -q Darwin;
then
  export BROWSER=open
else
  export BROWSER=google-chrome
fi

fpath=(/usr/local/share/zsh-completions $fpath)

# Prevent oh-my-zsh from auto-updating window titles
export DISABLE_AUTO_TITLE=true

# run command in a new tmux pane, while keeping
# the cursor at the current shell
# e.g. tmw watch -n 1 uptime
tmw() {
   tmux split-window -d -p 80 "$@"
}

# Use some sensible gtimeout defaults
gtimeout() {
  command gtimeout --foreground --preserve-status "$@"
}

# SSH to multiple hosts and
# turn on synchronization
function tmulti() {
  # if [ "$1" = "-e" ];
  # then
  #   shift
  #   tmux rename-window "mux"
  #   tssh $(nodeset -e "$@")
  # else
    for (( i=$#; i>0; i-- )); do
      tmux split-window -d -p$((100 / $i)) zsh -lc "$1"
      tmux select-layout tiled
      shift;
    done
    tmux setw synchronize-panes
    exit
  # fi
}

table() {
  tail +4 | ghead -n -1 | sed 's/ *| */\t/g'
}

watch() {
  command watch -n1 "zsh -l -c \"source ~/.zshrc; $@\""
}

alias ggpull='git pull --rebase origin $(current_branch)'
alias gd='git difftool'
gitweb() {
  branch=$(current_branch) || return
  $BROWSER $(sed -e "s#^.*\(git[^\.]*\.[^:\.]*\)[:\.]#https://\1/#" -e "s/\.git//" <(git config --get remote.origin.url)) 2>& /dev/null
}

travis() {
  branch=$(current_branch) || return
  $BROWSER $(sed -e "s#^.*\(git[^\.]*\.[^:\.]*\)[:\.]#https://travis-ci.com/#" -e "s#\.git##" <(git config --get remote.origin.url)) 2>& /dev/null
}

sample() {
  gsort -R | head -$1
}

swap() {
  local TMP="_$1.tmp_"
  mv $1 $TMP && mv $2 $1 &&  mv $TMP $2
}

alias ll='ls -lh'

# Expands into JS source files under current directory
# Excludes any files under node_modules, bower_components, or dist
#
# Example usage: jshint jss
alias -g jss='**/*.js~(*node_modules|dist|*styleguide)/*'

# Aggregates stats from numbers flowing into stdin
#
# Example usage: cat logs | grep endpoint | jq -r .took | stats
alias -g stats='datamash -H count 1 min 1 max 1 q3 1 perc:90 1 perc:98 1 perc:99 1 | column -t'

export BREW=/opt/homebrew
export PATH=~/.bin:~/.local/bin:$BREW/sbin:$BREW/bin:$PATH
export NODE_PATH=$BREW/lib/node_modules:$NODE_PATH
export GOPATH=~/gocode
# Among other things, this suppresses LOCALE warnings when launching VIM
export LC_ALL=en_US.UTF-8

# Bootstrap nvm
# Will set the node version when the working dir has .nvmrc
nvm use 2> /dev/null 1> /dev/null

# Boostrap rbenv
# Selects the Ruby version in folders with a .ruby-version file
which rbenv > /dev/null && eval "$(rbenv init -)"

if [[ -f /usr/bin/tmux || -f $BREW/bin/tmux ]]; then
  # attach to an already running tmux session or create new one
  # unless we're running a login shell in a virtual console or via SSH
  tty | grep -qo /dev/tty.$ || test "$SSH_TTY" || test "$TMUX" || ps -p $PPID | grep 'IntelliJ\|WebStorm' > /dev/null \
    || tmux attach 2>/dev/null || tmux new-session
fi

# bun completions
export BUN_INSTALL=~/.bun
export PATH=$BUN_INSTALL/bin:$PATH
[ -s ~/.bun/_bun ] && source ~/.bun/_bun

# zoxide
eval "$(zoxide init zsh)"

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"


alias claude="/Users/telsokhon/.claude/local/claude"
